# Maintainer: Luke Arms <luke@arms.to>
# Contributor: Jürgen Hötzel <juergen@archlinux.org>

pkgbase='ocaml-lk'
pkgname=('ocaml-lk' 'ocaml-compiler-libs-lk')
pkgver=4.12.1
pkgrel=1
pkgdesc="A functional language with OO extensions"
arch=('x86_64')
license=('LGPL2.1' 'custom: QPL-1.0')
url="https://caml.inria.fr/"
makedepends=('ncurses')
optdepends=('ncurses: advanced ncurses features' 'tk: advanced tk features')
provides=('ocaml')
conflicts=('ocaml')
source=("https://caml.inria.fr/distrib/ocaml-${pkgver%.*}/${pkgbase%-lk}-${pkgver}.tar.xz"
    'PR10266-dynamically-allocate-the-alternate-signal-stack.patch')
sha512sums=('ead6de8813c86a106f51cd9542d8076c8c21eb7885940313b8cbd21f5c32cf0f663fe871054e3b56bf0eeda848637233c42859f7cc76472fa8db1eac7be50bc4'
    '539d5da383c18c2769e27e394237d7c9a566ea2db5127b57e9e28c7c0a4801d3fb048b5e325675db5b6ce28d1032fb314e537b395f742b1790bfd8456059a03a')
options=('!makeflags' '!emptydirs' 'staticlibs')

build() {
    cd "${srcdir}/${pkgbase%-lk}-${pkgver}"
    CFLAGS+=' -ffat-lto-objects'
    CXXFLAGS+=' -ffat-lto-objects'
    # Backport glibc 2.34+ patch from https://github.com/ocaml/ocaml/pull/10266
    patch -p1 <"${srcdir}/PR10266-dynamically-allocate-the-alternate-signal-stack.patch"
    ./configure --prefix /usr --mandir /usr/share/man --disable-force-safe-string
    make --debug=v world.opt
}

package_ocaml-lk() {
    cd "${srcdir}/${pkgbase%-lk}-${pkgver}"
    make DESTDIR="${pkgdir}" install

    # Save >10MB with this one, makepkg only strips debug symbols.
    #find "${pkgdir}/usr/lib" -type f -name '*.so.*' -exec strip --strip-unneeded {} \;

    # install license
    install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname%-lk}"
    install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname%-lk}/"

    # remove compiler libs
    rm -rf "${pkgdir}/usr/lib/ocaml/compiler-libs"
}

package_ocaml-compiler-libs-lk() {
    pkgdesc="Several modules used internally by the OCaml compiler"
    license=('custom: QPL-1.0')
    depends=('ocaml-lk')
    optdepends=()
    provides=('ocaml-compiler-libs')
    conflicts=('ocaml-compiler-libs')

    cd "${srcdir}/${pkgbase%-lk}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    # Remove non-compiler-libs
    rm -rf "${pkgdir}/usr/bin" "${pkgdir}/usr/lib/ocaml/caml" \
        "${pkgdir}/usr/lib/ocaml/ocamldoc" "${pkgdir}/usr/lib/ocaml/stublibs" \
        "${pkgdir}/usr/lib/ocaml/threads" "${pkgdir}/usr/share"
    find "${pkgdir}/usr/lib/ocaml/" -maxdepth 1 -type f -delete

    install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname%-lk}"
    install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname%-lk}/"
}
