pkgname=devilspie2-lk
_pkgname=devilspie2
pkgver=0.44
pkgrel=1
pkgdesc="A window matching utility, allowing the user to perform scripted actions on windows as they are created."
arch=('i686' 'x86_64')
url="http://www.nongnu.org/$_pkgname/"
license=('GPL3')
depends=('lua>=5.1' 'gtk3' 'libwnck3')
source=("http://download.savannah.nongnu.org/releases/$_pkgname/${_pkgname}-$pkgver.tar.xz"{,.sig}
    'devilspie2-0.44-threads.patch')
sha256sums=('0a9f1eadd2b22a318163e4180065d495221ba1a43ad2021ea6866cd118042640'
    'SKIP'
    '0840bb368fff59601607ccac0b14614aaf8ab2f0b73cb90abe0cd02fa4cce1c4')
validpgpkeys=('A523530DD1E9FDDFAD3D5FCAA9B57A926EF302F5') # Darren Salt <devspam@moreofthesa.me.uk>
provides=("$_pkgname")
conflicts=("$_pkgname")

prepare() {
    cd "$_pkgname-$pkgver"
    patch -p1 -i "$srcdir/$_pkgname-0.44-threads.patch"
}

build() {
    cd "$_pkgname-$pkgver"
    make
}

package() {
    cd "$_pkgname-$pkgver"
    make DESTDIR="$pkgdir/" PREFIX=/usr install

    # Install documentation
    mkdir -p -m755 "$pkgdir/usr/share/doc"
    cp -Rp doc/ "$pkgdir/usr/share/doc/$_pkgname"
    install -Dp -m644 README "$pkgdir/usr/share/doc/$_pkgname/README"
}

# vim: set ft=sh ts=4 sw=4 noet:
