pkgname=devilspie2-lk
_pkgname=devilspie2
pkgver=0.43
pkgrel=1
pkgdesc="A window matching utility, allowing the user to perform scripted actions on windows as they are created."
arch=('i686' 'x86_64')
url="http://www.nongnu.org/$_pkgname/"
license=('GPL3')
depends=('lua>=5.1' 'gtk3' 'libwnck3')
source=("http://download.savannah.nongnu.org/releases/$_pkgname/${_pkgname}_$pkgver-src.tar.gz"{,.asc}
    "$_pkgname-0.43-lua-5.4.patch"
    "$_pkgname-0.43-threads.patch")
md5sums=('26eed0b5b4af5c7e13c551eceaeab832'
    'SKIP'
    '8fd96127706934b138af50aaa2da0664'
    '241b8d943c69ff33657b313dd8a9e758')
validpgpkeys=('DB306E4B10FFD98EF4DB55D7194B631AB2DA2888') # Andreas RÃ¶nnquist <gusnan@gusnan.se>
provides=("$_pkgname")
conflicts=("$_pkgname")

prepare() {
    cd "$_pkgname-$pkgver"
    patch -p1 -i "$srcdir/$_pkgname-0.43-lua-5.4.patch"
    patch -p1 -i "$srcdir/$_pkgname-0.43-threads.patch"
}

build() {
    cd "$_pkgname-$pkgver"
    make
}

package() {
    cd "$_pkgname-$pkgver"
    make DESTDIR="$pkgdir/" PREFIX=/usr install

    # Install documentation
    mkdir -p -m755 "$pkgdir/usr/share/doc"
    cp -Rp doc/ "$pkgdir/usr/share/doc/$_pkgname"
    install -Dp -m644 README "$pkgdir/usr/share/doc/$_pkgname/README"
}

# vim: set ft=sh ts=4 sw=4 noet:
