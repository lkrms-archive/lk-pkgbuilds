# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: Luke Arms <luke@arms.to>

pkgname=xfce4-settings-lk
_pkgname=xfce4-settings
pkgver=4.16.1
pkgrel=2
pkgdesc="Settings manager of the Xfce desktop"
arch=('x86_64')
url="https://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
depends=('exo' 'garcon' 'libxfce4ui' 'xfconf' 'libnotify' 'libcanberra'
  'colord' 'libxklavier' 'adwaita-icon-theme' 'gnome-themes-extra')
makedepends=('intltool' 'xf86-input-libinput')
optdepends=('python: xfce4-compose-mail -- "mailto:" URI handling')
provides=('xfce4-settings')
conflicts=('xfce4-settings')
source=(https://archive.xfce.org/src/xfce/$_pkgname/${pkgver%.*}/$_pkgname-$pkgver.tar.bz2
  keyboard-correctly-initialize-setting.patch
  default-xsettings-xml.patch)
sha256sums=('bb28e1be7aa34d0edb1cfbaacc509a4267db56828b36cd6be312a202973635c6'
  '020da2ecaef7d387c6082fed419bd01808c8c44f79c68b1a9c87ee6f85508d22'
  '8e9a6c70ab0ceb5d91b637dc290768f8a47edb5d7b6e2eebc4459dbc4ee040d7')
prepare() {
  cd "$srcdir/$_pkgname-$pkgver"

  # https://gitlab.xfce.org/xfce/xfce4-settings/-/issues/310
  patch -Np1 -i ../keyboard-correctly-initialize-setting.patch

  # Enable Adwaita theme and font hinting by default
  patch -Np1 -i "$srcdir/default-xsettings-xml.patch"
}

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --disable-xrandr \
    --enable-xcursor \
    --enable-libnotify \
    --enable-libxklavier \
    --enable-pluggable-dialogs \
    --enable-sound-settings \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
