# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Tobias Kieslich <tobias funnychar archlinux.org>
# Contributor: Luke Arms <luke@arms.to>

pkgname=xfce4-power-manager-lk
_pkgname=xfce4-power-manager
pkgver=4.16.0
pkgrel=2
pkgdesc="Power manager for Xfce desktop"
arch=('x86_64')
url="https://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
depends=('libxfce4ui' 'upower' 'libnotify' 'xfce4-notifyd' 'hicolor-icon-theme')
optdepends=('xfce4-panel: for the Xfce panel plugin')
makedepends=('intltool' 'xfce4-panel')
provides=('xfce4-power-manager')
conflicts=('xfce4-power-manager')
source=(
    https://archive.xfce.org/src/xfce/$_pkgname/${pkgver%.*}/$_pkgname-$pkgver.tar.bz2
    idle.patch
)
sha256sums=(
    'eb9c587c01b502fa45a32e7fc2aba98fa6d8391475133883654f77e562c43bf3'
    '8cb869d95587f048bee8d03ccafa5a4bc7c0e761815972c66ccc4c07e69ad436'
)

build() {
    patch -d "$srcdir/$_pkgname-$pkgver" -p1 <idle.patch

    cd "$srcdir/$_pkgname-$pkgver"

    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --sbindir=/usr/bin \
        --libexecdir=/usr/lib \
        --localstatedir=/var \
        --disable-network-manager \
        --enable-polkit \
        --disable-debug
    make
}

package() {
    cd "$srcdir/$_pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
