# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgname=dbeaver-plugin-sshj-lk
_pkgname=${pkgname%-lk}
pkgver=1.0.87.202110030714
pkgrel=1
pkgdesc='DBeaver plugin for SSHv2 tunnels with Bouncy Castle algorithm'
arch=('any')
url='https://dbeaver.io/'
license=('Apache')
makedepends=('unzip')
depends=('dbeaver-plugin-sshj-lib')
provides=("${_pkgname}=${pkgver}")
conflicts=("${_pkgname}")
source=("https://dbeaver.io/update/ce/latest/plugins/org.jkiss.dbeaver.net.ssh.sshj_${pkgver}.jar"
        "https://dbeaver.io/update/ce/latest/features/org.jkiss.dbeaver.net.sshj.feature_${pkgver}.jar"
        "${_pkgname}.info")
sha256sums=('64092561de4c6503263ba0204b745c3e3cf109a2ff37f0d8baf2c91eba8f29c5'
            '2707de7ebce9c97ad511eb13bfc2f278a86260f4000448f0b173e1d8f42b1eea'
            '8c2e9fb3024b832d451fa1e2536308be18197d83ebbad4128f43edc33e50bf8e')
noextract=("org.jkiss.dbeaver.net.ssh.sshj_${pkgver}.jar"
           "org.jkiss.dbeaver.net.sshj.feature_${pkgver}.jar")

package() {
  # Install plugin
  install -m 755 -d "${pkgdir}/usr/lib/dbeaver/plugins"
  install -m 644 -t "${pkgdir}/usr/lib/dbeaver/plugins" \
    "org.jkiss.dbeaver.net.ssh.sshj_${pkgver}.jar"

  # Extract feature
  install -m 755 -d "${pkgdir}/usr/share/dbeaver/features/org.jkiss.dbeaver.net.sshj.feature_${pkgver}"
  unzip -q "${srcdir}/org.jkiss.dbeaver.net.sshj.feature_${pkgver}.jar" \
        -d "${pkgdir}/usr/share/dbeaver/features/org.jkiss.dbeaver.net.sshj.feature_${pkgver}"

  # Add configuration file (handled by the hook)
  install -m 755 -d "${pkgdir}/etc/dbeaver/bundles.d"
  install -m 644 "${srcdir}/${_pkgname}.info" "${pkgdir}/etc/dbeaver/bundles.d/20-${_pkgname}.info"
}
