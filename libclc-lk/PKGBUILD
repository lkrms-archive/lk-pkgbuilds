# Maintainer: Luke Arms <luke@arms.to>
# Contributor: Laurent Carlier <lordheavym@gmail.com>

pkgname=libclc-lk
_pkgname=libclc
pkgver=12.0.0
pkgrel=2
pkgdesc="Library requirements of the OpenCL C programming language"
arch=('any')
url="https://libclc.llvm.org/"
license=('MIT')
makedepends=('clang' 'llvm' 'cmake' 'ninja' 'python' 'git'
    'spirv-llvm-translator')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("git+https://github.com/llvm/llvm-project.git#tag=llvmorg-$pkgver"
    gfx908+.patch)
md5sums=('SKIP'
    '8a7edf1c1c1f61313ac78862c7660026')

prepare() {
    cd llvm-project/libclc
    patch -p2 -i "$srcdir/gfx908+.patch"
    mkdir build
}

build() {
    cd llvm-project/libclc/build

    cmake .. -G Ninja \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr
    ninja
}

package() {
    cd llvm-project/libclc/build

    DESTDIR="$pkgdir" ninja install
    install -Dm644 ../LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

}
