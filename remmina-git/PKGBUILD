# Maintainer: Matt Rhoades <dev@rhoatech.com>
# Contributor: Christian Hesse <mail@eworm.de>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com> ([community] package)

pkgname=remmina-git
pkgver=1.4.16.r47.g76fc87325
pkgrel=1
pkgdesc='The GTK Remmina Remote Desktop Client'
arch=(x86_64)
url='http://www.remmina.org/'
license=('GPL')
depends=('avahi' 'libgcrypt' 'libssh' 'vte3' 'libsodium' 'libappindicator-gtk3')
makedepends=('git' 'cmake' 'freerdp' 'libvncserver' 'spice-gtk' 'spice-protocol' 'telepathy-glib'
  'harfbuzz' 'xorgproto' 'gobject-introspection')
optdepends=('freerdp: RDP plugin'
  'libsecret: Secret plugin'
  'libvncserver: VNC plugin'
  'libxkbfile: NX plugin'
  'nxproxy: NX plugin'
  'spice-gtk: Spice plugin'
  'telepathy-glib: Telepathy plugin'
  'xorg-server-xephyr: XDMCP plugin'
  'gnome-terminal: external tools')
replaces=('remmina-plugins')
provides=('remmina' 'grdc' "grdc=${pkgver}" 'remmina-plugins')
conflicts=('remmina' 'grdc')
install=remmina.install
source=('git+https://gitlab.com/remmina/remmina.git')
sha256sums=('SKIP')

pkgver() {
  cd remmina/

  if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
    printf '%s.r%s.g%s' \
      "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[-_+]/./g' <<<${GITTAG})" \
      "$(git rev-list --count ${GITTAG}..)" \
      "$(git rev-parse --short HEAD)"
  else
    printf '0.r%s.g%s' \
      "$(git rev-list --count master)" \
      "$(git rev-parse --short HEAD)"
  fi
}

build() {
  cd remmina/

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DWITH_APPINDICATOR=ON \
    -DWITH_NEWS=OFF \
    .
  make
}

package() {
  cd remmina/

  make DESTDIR="${pkgdir}/" install
}
