# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Contributor: Matej Grabovsky <matej.grabovsky at gmail>
# Contributor: Kohei Suzuki <eagletmt@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Nathan Owe <ndowens.aur at gmail dot com>
# Contributor: George Giorgidze <giorgidze@gmail.com>
# Contributor: Massimiliano Brocchini <brocchini@netseven.it>
# Contributor: Tej Chajed <tchajed at mit dot edu>

pkgname=camlp5-lk
pkgver=8.0
_relname=rel8.00.02
pkgrel=3
arch=('x86_64')
pkgdesc='A preprocessor-pretty-printer of OCaml.'
url='https://camlp5.github.io/'
license=('BSD')
depends=('ocaml-lk')
makedepends=('ocaml-compiler-libs-lk')
provides=("${pkgname%-lk}")
conflicts=("${pkgname%-lk}")
source=("camlp5-${_relname}.tar.gz::https://github.com/camlp5/camlp5/archive/$_relname.tar.gz")
sha256sums=('3c8c5c574b739cd9e4f0fd4881625ec4c67b456b64fc04ed2882e89a724577fd')
options=('!strip' 'staticlibs')

build() {
  cd "${srcdir}/camlp5-${_relname}"

  ./configure \
    -prefix '/usr' \
    -mandir '/usr/share/man' \
    -strict

  make world.opt
}

package() {
  cd "${srcdir}/camlp5-${_relname}"

  make DESTDIR="${pkgdir}" install

  install -D -m644 etc/META "${pkgdir}/usr/lib/ocaml/camlp5"
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/camlp5/LICENSE"
}
